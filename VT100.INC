#IFNDEF VT100_INC
VT100_INC	EQU 1
#MSG "VT100.INC REVISION 09/15/2010"
#MSG "COPYRIGHT 2010 WILLIAM STAPLETON"
#MSG "THIS FILE MAY BE FREELY DISTRIBUTED AS LONG AS IT REMAINS UNCHANGED AND FULLY CREDITED."

#INCLUDE ASCII.INC

;COMMENT OUT THE FOLLOWING LINE TO INCLUDE THE FULL FILE IN THE LISTING
;	NOLIST

VT100_MAKE_VARIABLES MACRO
#IFNDEF VT100_VARS
VT100_VARS EQU 1
	JMP NEXT\@
#ENDIF
#IFNDEF VT100_PREFIX
VT100_PREFIX	DC.B ESC,'[',NULL
#ENDIF
#IFNDEF VT100_ATTRIBUTE_STRING
VT100_ATTRIBUTE_STRING DC.B '00000',NULL
#ENDIF
NEXT\@:
	ENDM

VT100_CLEAR_SCREEN MACRO
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX	
	ITOA #2,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
	PUTC #'J'
	ENDM

VT100_HOME MACRO	
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX	
	PUTC #'H'
	ENDM

VT100_CURSOR MACRO ;ROW.COLUMN
#IF \# >=2
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX	
	ITOA \1,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
	PUTC #$3B
	ITOA \2,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
	PUTC #'H'
#ENDIF
#IF \# != 2
#MSG "MACRO VT100_CURSOR REQUIRES EXACTLY 2 PARAMETERS WHICH RESOLVE INTO NUMBERS FOR CURSOR ROW AND COLUMN"
#MSG "BOTH ROW AND COLUMN NUMBER BEGIN WITH 1 FOR THE TOP-LEFT CORNER"
#ENDIF
	ENDM

VT100_SAVE_CURSOR MACRO
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX	
	PUTC #'s'
	ENDM

VT100_RESTORE_CURSOR MACRO
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX	
	PUTC #'u'
	ENDM

VT100_UP MACRO ;COUNT
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX
#IF \# >= 1	
	ITOA \1,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
	PUTC #'A'
#IF \# >= 2
#MSG "MACRO VT100_UP REQUIRES AT MOST 1 PARAMETER WHICH RESOLVE INTO THE NUMBERS OF ROWS TO MOVE"
#MSG "IF NO PARAMETER IS SPECIFIED, THE DEFAULT IS USED (TYPICALLY 1 ROW)"
#ENDIF
	ENDM

VT100_DOWN MACRO ;COUNT
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX
#IF \# >= 1	
	ITOA \1,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
	PUTC #'B'
#IF \# >= 2
#MSG "MACRO VT100_DOWN REQUIRES AT MOST 1 PARAMETER WHICH RESOLVE INTO THE NUMBERS OF ROWS TO MOVE"
#MSG "IF NO PARAMETER IS SPECIFIED, THE DEFAULT IS USED (TYPICALLY 1 ROW)"
#ENDIF
	ENDM

VT100_RIGHT MACRO ;COUNT
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX
#IF \# >= 1	
	ITOA \1,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
	PUTC #'C'
#IF \# >= 2
#MSG "MACRO VT100_RIGHT REQUIRES AT MOST 1 PARAMETER WHICH RESOLVES INTO THE NUMBERS OF COLUMNS TO MOVE"
#MSG "IF NO PARAMETER IS SPECIFIED, THE DEFAULT IS USED (TYPICALLY 1 COLUMN)"
#ENDIF
	ENDM

VT100_LEFT MACRO ;COUNT
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX
#IF \# >= 1	
	ITOA \1,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
	PUTC #'D'
#IF \# >= 2
#MSG "MACRO VT100_LEFT REQUIRES AT MOST 1 PARAMETER WHICH RESOLVES INTO THE NUMBERS OF COLUMNS TO MOVE"
#MSG "IF NO PARAMETER IS SPECIFIED, THE DEFAULT IS USED (TYPICALLY 1 COLUMN)"
#ENDIF
	ENDM

VT100_DEFAULT 	EQU 0
VT100_BRIGHT	EQU 1
VT100_DIM		EQU 2
VT100_UNDERLINE EQU 4
VT100_BLINK	EQU 5
VT100_INVERT	EQU 7
VT100_HIDDEN	EQU 8
VT100_FOREGROUND_PREFIX EQU 30
VT100_BACKGROUND_PREFIX EQU 40
VT100_BLACK	EQU 0
VT100_RED		EQU 1
VT100_GREEN 	EQU 2
VT100_YELLOW 	EQU 3		;YELLOW=RED+GREEN		
VT100_BLUE		EQU 4
VT100_MAGENTA	EQU 5		;MAGENTA=BLUE+RED	
VT100_CYAN		EQU 6		;CYAN=BLUE+GREEN		
VT100_WHITE	EQU 7		;WHITE=BLUE+GREEN+RED	


VT100_TEXT_ATTRIBUTE MACRO ;PARAMETERS
	VT100_MAKE_VARIABLES
	PUTS #VT100_PREFIX
#IF \# = 0
	ITOA #VT100_DEFAULT,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
#IF \# >=1	
	ITOA \1,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
#IF \# >=2
	PUTC #$3B	
	ITOA \2,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
#IF \# >=3
	PUTC #$3B	
	ITOA \3,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
#IF \# >=4
	PUTC #$3B	
	ITOA \4,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
#IF \# >=5
	PUTC #$3B	
	ITOA \5,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
#IF \# >=6
	PUTC #$3B	
	ITOA \6,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
#IF \# >=7
	PUTC #$3B	
	ITOA \7,#VT100_ATTRIBUTE_STRING
	PUTS #VT100_ATTRIBUTE_STRING
#ENDIF
	PUTC #'m'
#IF \# =0 | \# > 7
#MSG "MACRO VT100_TEXT_ATTRIBUTE REQUIRES UP TO 7 PARAMETERS WHICH RESOLVE INTO VT100 TEXT ATTRIBUTES"
#MSG "EXAMPLE ATTRIBUTES ARE VT100_BRIGHT, VT100_UNDERLINE, AND VT100_BLINK"
#MSG "EXAMPLE TO SET BACKGROUND COLOR USE: VT100_TEXT_ATTRIBUTE #(VT100_BACKGROUND_PREFIX+VT100_RED)"
#MSG "EXAMPLE TO SET FOREGROUND COLOR USE: VT100_TEXT_ATTRIBUTE #(VT100_FOREGROUND_PREFIX+VT100_GREEN)"
#MSG "VT100_DEFAULT REMOVES ALL OTHER ATTRIBUTES, INCLUDING TEXT COLOR"
#MSG "MORE THAN 7 PARAMETERS AT A TIME IS DISALLOWED"
#MSG "PROVIDING 0 PARAMETERS IS EQUIVALENT TO SETTING VT100_DEFAULT"
#ENDIF
	ENDM







;THE FOLLOWING LINE RESTORES LISTING FOR CODE OUTSIDE OF THIS LIBRARY
	LIST

#ENDIF	
